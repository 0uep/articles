---
URL:     https://linuxfr.org/news/python-partie-5-nix-et-guix
Title:   Python 2019/2021 ‚Äî partie 5 ‚Äî Nix (et Guix)
Authors: nokomprendo
         Oliver, Ysabeau, palm123, tisaac et gusterhack
Date:    2019-10-21T13:16:25+02:00
License: CC By-SA
Tags:    python, nix, guix, python3, anaconda, docker et podman
Score:   16
---

Python 2019/2021 ‚Äî partie 5 ‚Äî Nix (et Guix)
===========================================

Dans les pr√©c√©dentes d√©p√™ches, nous avons discut√© de la [popularit√© de Python](https://linuxfr.org/news/python-pour-la-rentree-2019-partie-1), la [fin de la maintenance de Python 2](https://linuxfr.org/news/python-pour-la-rentree-2019-partie-2), les [diff√©rentes variantes](https://linuxfr.org/news/python-pour-la-rentree-2019-partie-3-installation-de-python-et-de-paquets) de Python, comment les faire [cohabiter avec Py et Pipenv](https://linuxfr.org/news/python-pour-la-rentree-2019-partie-4-py-pyenv)‚Ä¶

Dans la continuit√© de la pr√©c√©dente d√©p√™che, cette d√©p√™che pr√©sente une autre approche pour faire cohabiter diff√©rentes versions de Python sur un m√™me ordinateur‚ÄØ: [Nix](https://fr.wikipedia.org/wiki/Nix). üöÄ‚ÄØüêç‚ÄØüí´ [![Le logo de Python entour√© de petites ic√¥nes symbolisant la vari√©t√© des domaines o√π s‚Äôapplique Python, et √† droite, un joyeux barbu se tient derri√®re un √©cran d‚Äôordinateur qui affiche ¬´ partie = 5, "Conda Docker" \n print(partie) ¬ª](05.webp)](https://github.com/linuxfr.org/articles/tree/main/python/2019-2021)

----

* [Python ‚Äï partie 1 ‚Äï Popularit√©](https://linuxfr.org/news/python-pour-la-rentree-2019-partie-1)
* [Python ‚Äï partie 2 ‚Äï Python 2](https://linuxfr.org/news/python-pour-la-rentree-2019-partie-2)
* [Python ‚Äï partie 3 ‚Äï Installation de¬†Python et¬†de¬†paquets](https://linuxfr.org/news/python-pour-la-rentree-2019-partie-3-installation-de-python-et-de-paquets)
* [Python ‚Äï partie 4 ‚Äï Py Pyenv](https://linuxfr.org/news/python-pour-noel-2019-partie-4-py-pyenv)
* [Python ‚Äî partie 7 ‚Äî Environnements virtuels](https://linuxfr.org/news/python-pour-noel-202x-partie-7-environnements-virtuels)

----

Gestionnaires de paquets √©volu√©s (Nix, Guix)
============================================

[Nix](https://fr.wikipedia.org/wiki/Nix) et [Guix](https://fr.wikipedia.org/wiki/GNU_Guix) sont des gestionnaires de paquets g√©n√©ralistes mais tr√®s pratiques pour d√©velopper en Python. L‚Äôid√©e de base est d‚Äôappliquer les principes de la programmation fonctionnelle √† la gestion de paquets. Ainsi, un paquet est vu comme une fonction qui prend l‚Äôenvironnement courant et le code source d‚Äôun logiciel, et retourne un nouvel environnement o√π le nouveau logiciel est disponible. Cette approche simpifie la reproduction, la r√©utilisation et l‚Äôisolation des environnements logiciels cr√©√©s. 

Avec Nix
--------

### Cr√©er un environnement Python simple

Nix permet de cr√©er des environnements temporaires. Par exemple, la commande suivante lance l‚Äôinterpr√©teur Python en incluant les modules Numpy et Pandas¬†:

```
$ nix-shell -p 'python3.withPackages(ps: with ps; [ numpy pandas ])' --run python
Python 3.7.5 (default, Oct 14 2019, 23:08:55) 
[GCC 8.3.0] on linux
Type "help", "copyright", "credits" or "license" for more information.
>>> 
```

Ici, Python et les modules Numpy et Pandas sont fournis par le d√©p√¥t de paquets associ√© √† Nix. Le d√©p√¥t standard `nixpkgs` propose une version de Python 3 et une version de Python 2, avec les modules correspondants. Il est √©galement possible d‚Äôutiliser son propre d√©p√¥t personnalis√© ou de personnaliser sp√©cifiquement quelques paquets du d√©p√¥t standard (comme expliqu√© dans la suite de cette d√©p√™che).

Lorsqu‚Äôon travaille sur un projet particulier, on pr√©f√®re g√©n√©ralement ajouter un fichier `shell.nix`, qui d√©crit l‚Äôenvironnement √† cr√©er pour le projet :

```nix
with import <nixpkgs> {};
(python3.withPackages (ps: with ps; [
  numpy
  pandas
])).env
```

Une fois ce fichier cr√©√©, il suffit de lancer la commande `nix-shell` pour cr√©er l‚Äôenvironnement correspondant.

```
$ nix-shell --run python
Python 3.7.5 (default, Oct 14 2019, 23:08:55) 
[GCC 8.3.0] on linux
Type "help", "copyright", "credits" or "license" for more information.
>>> 
```

Ce simple fichier `shell.nix` est d√©j√† tr√®s pratique car il permet de d√©finir de fa√ßon pr√©cise et homog√®ne l‚Äôenvironnement logiciel d‚Äôun projet Python. De plus, les d√©pendances sont mutualis√©es : si un autre projet n√©cessite Numpy, ce sont exactement les m√™mes fichiers Numpy qui seront utilis√©s. Enfin, Nix permet de g√©rer d‚Äôautres types de paquets que Python et de personnaliser finement l‚Äôenvironnement, si besoin.

### Empaqueter un projet Python, avec toutes ses d√©pendances

Pour construire un projet Python, on √©crit g√©n√©ralement un fichier `setup.py`. Nix poss√®de une fonction `buildPythonPackage` pour cr√©er un paquet Nix √† partir d‚Äôun fichier `setup.py`. L‚Äôint√©r√™t d‚Äôutiliser Nix en plus du fichier Python est que l‚Äôon peut d√©finir pr√©cis√©ment toutes les d√©pendances (Python ou non) et int√©grer notre projet au syst√®me Nix (et donc de pouvoir le r√©utiliser directement dans un autre projet).

Par exemple, imaginons que nous avons un projet Python "myapp" qui utilise le paquet Python Numpy, le paquet syst√®me zlib et une biblioth√®que interne C++ "mylibcpp" (elle-m√™me empaquet√©e via Nix). Alors, on peut empaqueter notre projet avec le fichier `default.nix` suivant :

```nix
{ pkgs ? import <nixpkgs> {} }:

let
  mylibcpp = pkgs.callPackage ./mylibcpp/default.nix {};
in
pkgs.python3Packages.buildPythonPackage {
  name = "myapp";
  src = ./.;
  propagatedBuildInputs = [
    pkgs.python3Packages.numpy  # paquet python
    pkgs.zlib                   # paquet non python
    mylibcpp                    # paquet perso
  ];
}
```

Ce fichier `default.nix` nous permet de :

- lancer un `nix-shell` et avoir toutes les d√©pendances n√©cessaires pour travailler sur notre projet Python;
- lancer un `nix-build` pour construire et ex√©cuter une version finale de notre projet;
- lancer un `nix-env -i -f .` pour installer notre projet sur le syst√®me, comme n‚Äôimporte quel autre paquet de la logith√®que de Nix;
- r√©utiliser notre projet dans un autre projet utilisant Nix;
- et m√™me, de soumettre notre projet pour une int√©gration dans la logith√®que de Nix.

### Modifier l‚Äôenvironnement Python standard

Nix permet de configurer compl√®tement l‚Äôenvironnement logiciel, notamment d‚Äôajouter des paquets Python suppl√©mentaires. Par exemple, si on veut r√©cup√©rer le paquet Cma sur Pypi et l‚Äôint√©grer √† l‚Äôenvironnement, on peut √©crire le fichier `shell.nix` suivant :

```nix
with import <nixpkgs> {};

( let
  cma = python3Packages.buildPythonPackage rec {
    pname = "cma";
    version = "2.7.0";
    src = python3Packages.fetchPypi {
      inherit pname version;
      sha256 = "0nfdq7qznfqcnqwbk04812xiddq8azzbvdj2pa2lvgzxbr00sqzl";
    };
    propagatedBuildInputs = [
      python3Packages.numpy
    ];
    doCheck = false;
  };
  in python3.withPackages (ps: [ cma ])
).env
```

Il est √©galement possible de red√©finir l‚Äôenvironnement courant. Par exemple, le fichier `shell.nix` suivant fixe Numpy √† la version 1.16.4 et utilise cette version dans le nouvel environnement, notamment pour le paquet Cma, √©galement ajout√©.

```nix
with import <nixpkgs> {};

(let
  python = let
    packageOverrides = self: super: {

      numpy = self.buildPythonPackage rec {
        pname = "numpy";
        version = "1.16.4";
        src = fetchTarball {
          url = "https://github.com/numpy/numpy/releases/download/v${version}/numpy-${version}.tar.gz";
          sha256 = "1wparj9r5cnh9290s2ikp28yvhx95mainnnlxjgw5p6mppk5zkix";
        };
        doCheck = false;
      };

      cma = self.buildPythonPackage rec {
        pname = "cma";
        version = "2.7.0";
        src = self.fetchPypi {
          inherit pname version;
          sha256 = "0nfdq7qznfqcnqwbk04812xiddq8azzbvdj2pa2lvgzxbr00sqzl";
        };
        propagatedBuildInputs = [
          self.numpy
        ];
        doCheck = false;
      };

    };
  in pkgs.python3.override {
        inherit packageOverrides;
        self = python;
      };

in python.withPackages(ps: [ ps.cma ])).env
```

Avec Guix
---------

Le gestionnaire de paquets [GNU Guix](https://fr.wikipedia.org/wiki/GNU_Guix), inspir√© de Nix, offre une solution similaire avec la commande [`guix environment`](https://guix.gnu.org/manual/en/html_node/Invoking-guix-environment.html).

Remerciements
=============

Merci √† tou‚ãÖte‚ãÖs les contribut‚ãÖrices‚ãÖeurs de cette d√©p√™che, et notamment √† [Oliver](https://linuxfr.org/users/oliver) pour l‚Äôavoir initi√©e.‚ÄØüëè Continuons √† participer aux prochaines d√©p√™ches. üëç

Licence
=======

Cette d√©p√™che est publi√©e sous [licence CC0](https://fr.m.wikipedia.org/wiki/Licence_CC0) (dans le domaine public dans les pays o√π c‚Äôest possible) pour permettre de la recopier, modifier, r√©utiliser et republier sans obligation de citer ses auteurs quelle que soit la loi de certains pays comme la France, qui oblige quand m√™me √† citer les auteurs. Au moins, cela montre l‚Äôintention des auteurs √† autoriser le plagiat.

Tes astuces ?
=============

N‚Äôh√©site pas √† partager tes exp√©riences, tes astuces et tes interrogations.

Donne-nous un coup de main pour la r√©daction des d√©p√™ches √† venir.
